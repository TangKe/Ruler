apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    namespace 'ke.tang.ruler'
    compileSdkVersion versions.compileSdkVersion

    defaultConfig {
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode versions.versionCode
        versionName versions.versionName
        aarMetadata {
            minCompileSdk 28
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

publishing {
    publications {
        release(MavenPublication) {
            artifactId = 'Ruler'
            groupId = 'com.github.Tangke'
            version = versions.versionName
            afterEvaluate {
                from components.release
            }
            pom {
                name = 'ruler'
                description = 'A virtual ruler widget for android'
                url = 'http://github.com/TangKe/Ruler'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'tangke'
                        name = 'Tang Ke'
                        email = 'tang.ke@me.com'
                    }
                }
                scm {
                    url = 'http://github.com/TangKe/Ruler.git'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:${versions.library.appCompat}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.library.kotlin}"
}